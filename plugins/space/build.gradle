buildscript {
    repositories {
        maven { url "https://dl.bintray.com/jetbrains/intellij-plugin-service" }
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.3.12"
}

applyKotlinJVM()

if (hasProperty("teamcity")) {
    version = teamcity["build.number"]
}

dependencies {
    compile project(":app:app-state")

    compile "io.ktor:ktor-server-core:$ktor_version"
    compile "io.ktor:ktor-server-jetty:$ktor_version"
    compile "org.slf4j:jul-to-slf4j:$sl4j_version"
    compile "ch.qos.logback:logback-classic:1.2.1"

    compile "org.glassfish.jersey.core:jersey-common:2.23"
    compile "org.glassfish.jersey.core:jersey-client:2.23"

    compile "nl.komponents.kovenant:kovenant:3.3.0"
    compile "commons-io:commons-io:$apache_commons_io_version"
}

test { finalizedBy jacocoTestReport }

intellij {
    version "IC-173.4127.17"
    pluginName "circlet-intellij-plugin"
    updateSinceUntilBuild false
    sandboxDirectory "${project.projectDir}/.sandbox"
}

buildPlugin {
    doLast {
        def buildSettings = Build.buildSettings
        if (buildSettings.isCIRun()){
            def buildNumber = buildSettings.buildNumber

            new File("$projectDir/build/distributions/update.xml").write("""<!-- Plugin repository URL: "http://buildserver.labs.intellij.net/guestAuth/repository/download/Circlet_IdeaCircletIntegration/lastSuccessful/update.xml?redirectSupported=false" -->
<plugins>
    <plugin id="com.jetbrains.circlet" url="http://buildserver.labs.intellij.net/guestAuth/repository/download/Circlet_IdeaCircletIntegration/$buildNumber/app-idea-${buildNumber}.zip?redirectSupported=false" version="$buildNumber">
        <name>Circlet Integration</name>
        <description>Circlet Integration plugin</description>
    </plugin>
</plugins>""")
            println "##teamcity[publishArtifacts 'app/app-idea/build/distributions/update.xml']"
        }
    }
}
