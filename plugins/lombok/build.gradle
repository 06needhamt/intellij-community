plugins {
    id 'org.jetbrains.intellij' version '0.0.43'
    id 'de.undercouch.download' version '2.1.0'
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'

sourceCompatibility = javaVersion
targetCompatibility = javaTargetVersion

group pluginGroup
version = pluginVersion

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'

    if (!Boolean.valueOf(System.getenv('CI'))) {
        def javaHome = System.getenv("JAVA_HOME")
        if (null == javaHome || "".equals(javaHome)) {
            javaHome = JDK6_HOME
        }

        options.bootClasspath = "$javaHome/jre/lib/rt.jar"
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java',
                       'generated/src']
        }
        resources {
            srcDirs = ['src/main/resources',
                       'generated/resources']
        }
    }
}

intellij {
    def ideaVersionExternDefinition = System.getenv("IDEA_VERSION")
    version = ideaVersionExternDefinition ? ideaVersionExternDefinition : ideaVersion

    pluginName pluginName
    downloadSources Boolean.valueOf(sources)
    sameSinceUntilBuild Boolean.valueOf(isEAP)
    alternativeIdePath idePath
    updateSinceUntilBuild false

    sandboxDirectory = "${project.projectDir}/idea-sandbox/idea-${version}"

    plugins 'coverage'

    publish {
        pluginId publishPluginId
        channel publishChannel
        username publishUsername
        password System.properties['intellij.publish.password']
    }
}

idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
}

plugins.withType(JavaPlugin) {
    test {
        systemProperty "NO_FS_ROOTS_ACCESS_CHECK", "true"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

// Configurations
configurations {
    bnf
    jflex

    lombok
    hrisey
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    lombok group: 'org.projectlombok', name: 'lombok', version: '1.16.8', classifier: 'sources', ext: 'jar'
    hrisey group: 'pl.mg6.hrisey', name: 'hrisey', version: '0.3.1', classifier: 'sources', ext: 'jar'

    bnf files("$buildDir/tools/GrammarKit/lib/grammar-kit.jar")
    bnf files("$buildDir/tools/light-psi-all.jar")
    jflex files("$buildDir/tools/JFlex.jar")

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.+'
}

import de.undercouch.gradle.tasks.download.Download

// Tasks
copy {
    includeEmptyDirs = false
    from({ zipTree(configurations.lombok.singleFile) }) {
        include 'lombok/extern/**'
        include 'lombok/experimental/**'
        include 'lombok/Builder.java'
        include 'lombok/AccessLevel.java'
        include 'lombok/*ArgsConstructor.java'
        include 'lombok/Cleanup.java'
        include 'lombok/Data.java'
        include 'lombok/Delegate.java'
        include 'lombok/Getter.java'
        include 'lombok/Setter.java'
        include 'lombok/Equals*.java'
        include 'lombok/NonNull.java'
        include 'lombok/Synchronized.java'
        include 'lombok/ToString.java'
        include 'lombok/SneakyThrows.java'
        include 'lombok/Value.java'
        include 'lombok/val.java'
        include 'lombok/Singular.java'
        include 'lombok/core/handlers/Singulars.java'
    }
    into("generated/src")
}

copy {
    includeEmptyDirs = false
    from({ zipTree(configurations.hrisey.singleFile) }) {
        include 'hrisey/Parcelable.java'
        include 'hrisey/Preferences.java'
    }
    into("generated/src")
}

copy {
    includeEmptyDirs = false
    from({ zipTree(configurations.lombok.singleFile) }) {
        include '**/*.txt'
    }
    into("generated/resources")
}


task downloadJFlexJar(type: Download) {
    description 'Downloads JFlex.'

    src 'https://github.com/JetBrains/intellij-community/raw/master/tools/lexer/jflex-1.4/lib/JFlex.jar'
    dest "$buildDir/tools/JFlex.jar"
    onlyIf { !file("$buildDir/tools/JFlex.jar").exists() }
}

task downloadJFlexSkeleton(type: Download) {
    description 'Downloads JFlex skeleton.'

    src 'https://raw.github.com/JetBrains/intellij-community/master/tools/lexer/idea-flex.skeleton'
    dest "$buildDir/tools/idea-flex.skeleton"
    onlyIf { !file("$buildDir/tools/idea-flex.skeleton").exists() }
}

task downloadGrammarKit(type: Download) {
    description 'Downloads GrammarKit.'

    src 'https://github.com/JetBrains/Grammar-Kit/releases/download/1.2.0.1/GrammarKit.zip'
    dest "$buildDir/tools/GrammarKit.zip"
    onlyIfNewer true
}

task downloadGrammarLightPsi(type: Download) {
    description 'Downloads GrammarKit Light Psi.'

    src 'https://github.com/JetBrains/Grammar-Kit/releases/download/1.2.0.1/light-psi-all.jar'
    dest "$buildDir/tools/light-psi-all.jar"
    onlyIfNewer true
}

task unzipGrammarKit(type: Copy) {
    description 'Extracts GrammarKit files.'
    dependsOn downloadGrammarKit

    from zipTree("$buildDir/tools/GrammarKit.zip")
    into "${buildDir}/tools/"
    include "GrammarKit/lib/*"
}

task generateLexer(type: JavaExec) {
    description 'Generates lexer sources with JFlex.'
    dependsOn downloadJFlexJar, downloadJFlexSkeleton

    main = 'JFlex.Main'
    args = [
            '-nobak',
            '-sliceandcharat',
            '-skel',
            "$buildDir/tools/idea-flex.skeleton",
            '-d',
            "generated/src/de/plushnikov/intellij/plugin/language/",
            "$projectDir/src/main/resources/de/plushnikov/intellij/plugin/language/lombokConfig.flex",
    ]

    classpath = configurations.jflex
}

task generateParser(type: JavaExec) {
    description 'Generates parser sources with GrammarKit.'
    dependsOn unzipGrammarKit, downloadGrammarLightPsi

    main = 'org.intellij.grammar.Main'
    args = ["generated/src", file("src/main/resources/de/plushnikov/intellij/plugin/language/lombokConfig.bnf")]

    classpath = configurations.bnf + configurations.compile
}

task generateSources() {
    dependsOn generateParser, generateLexer
}

clean.doFirst {
    delete 'generated', 'out'
}

compileJava.dependsOn generateSources