{
  parserClass = 'com.intellij.json.JsonParser'
  parserUtilClass = "com.intellij.json.JsonParserUtil"
  psiPackage = 'com.intellij.json.psi'
  psiImplPackage = 'com.intellij.json.psi.impl'

  elementTypeHolderClass = 'com.intellij.json.JsonElementTypes'
  elementTypeClass = 'com.intellij.json.JsonElementType'
  psiClassPrefix = "Json"

  psiImplUtilClass = 'com.intellij.json.psi.impl.JsonPsiImplUtils'
  tokenTypeClass = 'com.intellij.json.JsonTokenType'

  implements = "com.intellij.json.psi.JsonElement"
  extends = "com.intellij.json.psi.impl.JsonElementImpl"

    tokens = [
        L_CURLY='{'
        R_CURLY='}'
        L_BRACKET='['
        R_BRACKET=']'

        COMMA=','
        COLON=':'
        LINE_COMMENT='regexp://.*'
        // "/*" ([^*]|\*+[^*/])* (\*+"/")?
        BLOCK_COMMENT='regexp:/\*([^*]|\*+[^*/])*(\*+/)?'
        // else /\*(?:[^*]|\*[^/])*\*+/

        // unclosed string literal matches till the line's end
        // any escape sequences included, illegal escapes are indicated by SyntaxHighlighter
        // and JsonStringLiteralAnnotator
        STRING="regexp:\"([^\\\"\r\n]|\\[^\r\n])*\"?|'([^\\\"\r\n]|\\[^\r\n])*'?"
//        STRING='regexp:"([^\\"\r\n]|\\([\\"/bfnrt]|u[a-fA-F0-9]{4}))*"?'

        NUMBER='regexp:-?\d+(\.\d+([eE][+-]?\d+)?)?'
        TRUE='true'
        FALSE='false'
        NULL='null'
        // Workaround for highlighting issue
        // plays role of IDENTIFIER or NAME token in other languages
        TEXT='regexp:\w+'
    ]

    extends("object|array|literal")=value
    extends("string_literal|number_literal|boolean_literal|null_literal")=literal
    implements("property")=[
      "com.intellij.json.psi.JsonElement"
      "com.intellij.psi.PsiNamedElement"
    ]
}

json ::= object | array

object ::= '{' object_element* '}' {
  pin=1
  methods=[
    findProperty
  ]
}

// Hackity-hack to parse array elements and properties even if separating commas are missing,
// TODO: Find out if there is any simpler way to do so in GrammarKit
private object_element ::= property (','|&'}') {
  recoverWhile = not_brace_or_next_value
  pin = 1
}

property ::= string_literal (':' value) {
  methods=[
    getName
    getNameElement
    getValue
    // suppress getValueList() accessor
    value=""
  ]
  mixin="com.intellij.json.psi.impl.JsonPropertyMixin"
  pin(".*")=1
}

array ::= '[' array_element* ']' { pin=1 }

private array_element ::= value (','|&']') {
  recoverWhile = not_bracket_or_next_value
  pin=1
}

string_literal ::= STRING
number_literal ::= NUMBER
boolean_literal ::= TRUE | FALSE
null_literal ::= NULL

literal ::= string_literal | number_literal | boolean_literal | null_literal {
  methods=[isQuotedString]
  mixin="com.intellij.json.psi.impl.JsonLiteralMixin"
}
value ::= object | array | literal

// Recoveries
private not_bracket_or_next_value ::= !(']'|value)
private not_brace_or_next_value ::= !('}'|value)