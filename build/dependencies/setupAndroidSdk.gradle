/*
 * Copyright 2000-2019 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

repositories {
  maven { url 'https://cache-redirector.jetbrains.com/jetbrains.bintray.com/intellij-third-party-dependencies/' }
  ivy {
    // TODO: re-publish android-sdk:android-sdk using maven layout and remove Ivy repository
    url 'https://cache-redirector.jetbrains.com/jetbrains.bintray.com/intellij-third-party-dependencies/'
    patternLayout {
      artifact "[module]/[revision]/[artifact].[ext]"
    }
  }
}

configurations {
  androidOfflineRepo
  androidSdk
}

def currentOs = org.gradle.internal.os.OperatingSystem.current()
def osFamily = currentOs.familyName

dependencies {
  androidOfflineRepo "org.jetbrains.intellij.deps.android.tools:android-sdk-offline-repo:26.5.0@zip"
  androidSdk "android-sdk:android-sdk:${osFamily}.3.3.3:@tar.gz"
}

/* First we setup an offline-repo. This will remove all the alien files from .../android-sdk.
   Later we will create an overlay with Android SDK and KotlinPlugin artifacts */
task syncOfflineRepo(dependsOn: configurations.androidOfflineRepo, type: Sync) {
  from zipTree(configurations.androidOfflineRepo.singleFile)
  into "${project.buildDir}/android-sdk"
}

task syncKotlinPluginForAndroid(dependsOn: [setupKotlinPlugin, syncOfflineRepo], type: Sync) {
  from "$buildDir/kotlin"
  into "$buildDir/android-sdk/prebuilts/tools/common/kotlin-plugin"
}

task syncAndroidSdk(dependsOn: [configurations.androidSdk, syncOfflineRepo], type: Sync) {
  from tarTree(resources.gzip(configurations.androidSdk.singleFile))
  into "$buildDir/android-sdk/prebuilts/studio/sdk"
}

task setupAndroidSdk(dependsOn: [syncOfflineRepo, syncKotlinPluginForAndroid, syncAndroidSdk]) {
  // This is entry point: invoke this task to setup android sdk and all the artifacts required to run android-plugin unit tests
}
