/*
 * Copyright 2000-2016 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.util.regex.Pattern

target('default': 'Downloads custom JRE build from Teamcity server') {
  def buildId = new URL("$jreTeamcityUrl/buildTypes/id:$jreBuildTypeId/builds/tags:$jreBuildTag/id").text
  def buildNumber = new URL("$jreTeamcityUrl/builds/id:$buildId/number").text
  java(jvm: jreArtifactPlatform == "mac" ? "$workIdeaHome/jre/jdk/Contents/Home/jre/bin/java" : "$workIdeaHome/jre/jre/bin/java",
       fork: "true", classname: "dummy", outputproperty: "jvmVersionOutput") {
    jvmarg(value: "-version")
  }
  try {
    if (jvmVersionOutput =~ /\(build .*release-b${Pattern.quote(buildNumber)}\)/ &&
        jvmVersionOutput =~ (jreArchitecture == "64" ? /OpenJDK 64-Bit Server VM/ : /OpenJDK Server VM/)) {
      echo "Latest version of custom JRE is installed, no update is required"
      return
    }
  }
  catch (MissingPropertyException ignored) {
    echo "Could'nt determine existing JRE version"
  }
  def files = new XmlSlurper().parse("$jreTeamcityUrl/builds/id:$buildId/artifacts/children/$jreArtifactPlatform")
  def artifact = files.children().collect { it.@name.text() }.find {
    it.startsWith(jreArtifactPrefix) && it.endsWith(jreArchitecture == "64" ? "_x64.tar.gz" : "_x86.tar.gz")
  }
  get src: "$jreTeamcityUrl/builds/id:$buildId/artifacts/content/$jreArtifactPlatform/$artifact", dest: deployDir
  mkdir dir: "$deployDir/jre"
  untar src: "$deployDir/$artifact", dest: "$deployDir/jre", compression: "gzip"
  delete file: "$deployDir/$artifact"
}