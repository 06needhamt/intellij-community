import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id "org.jetbrains.intellij" version "0.0.39"
}

subprojects {
  apply plugin: 'java'
  apply plugin: "org.jetbrains.intellij"

  tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

  sourceSets {
    main {
      java.srcDirs 'src', '../src', '../gen'
      resources.srcDir 'resources'
    }
    test {
      java.srcDir 'test'
    }
  }

  processResources {
    filter ReplaceTokens, tokens: [
      'VERSION': version.toString(),
      'BUILD-NUMBER': buildNumber,
    ]
  }

  repositories {
    ivy {
      ivyPattern      'http://buildserver.labs.intellij.net/guestAuth/repository/download/ijplatform_master_PyCharm_Installers/lastSuccessful/teamcity-ivy.xml'
      artifactPattern 'http://buildserver.labs.intellij.net/guestAuth/repository/download/ijplatform_master_PyCharm_Installers/lastSuccessful/pycharmPC-'+pycharmVersion+'.zip'
    }
  }

  configurations {
    pycharm
  }

  dependencies {
    pycharm ('org:ijplatform_master_PyCharm_Installers:lastSuccessful')
    compileOnly fileTree(dir: configurations.pycharm.singleFile.parent + "/pycharm/lib/", include: ['**/*.jar'])
  }

  task extractPyCharm(type: Copy) {
    from zipTree(configurations.pycharm.singleFile)
    into configurations.pycharm.singleFile.parent + "/pycharm"
    include "**/*.jar"
  }

  intellij {
    version ideaVersion
    updateSinceUntilBuild true
    downloadSources Boolean.valueOf(downloadIdeaSources)
    sandboxDirectory = new File(rootProject.projectDir, "gradleBuild/idea-sandbox")
  }

  buildDir = new File(rootProject.projectDir, "gradleBuild/" + project.name)
}

project(':student-python') {

  dependencies {
    compile project(':educational-core:student') {
      dependencies {
        compile fileTree(dir: 'lib', include: ['*.jar'])
      }
    }
  }

  compileJava.dependsOn(extractPyCharm)

  intellij {
    pluginName 'student-python'
  }
}


project(':course-creator-python') {

  dependencies {
    compile project(':student-python')

    compile project(':educational-core:course-creator') {
      dependencies {
        compile project(':educational-core:student')
      }
    }
  }

  intellij {
    pluginName 'course-creator-python'
  }

}